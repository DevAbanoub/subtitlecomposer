# Maintainer: Mladen Milinkovic <maxrd2@smoothware.net>
# Contributor: Mladen Milinkovic <maxrd2@smoothware.net>
# Contributor: Martchus <martchus@gmx.net>

# You can install/update Subtitle Composer from repository if you add following to /etc/pacman.conf
# [subtitlecomposer]
# # Subtitle Composer
# SigLevel = PackageRequired
# Server = http://smoothware.net/$repo/$arch

_name=subtitlecomposer
pkgname=${_name}-git
pkgver=v0.5.9
pkgrel=1
pkgdesc="A KDE subtitle editor (git version)"
arch=('i686' 'x86_64')
url="https://github.com/maxrd2/${_name}"
license=('GPL')
depends=('kcoreaddons' 'sonnet' 'kcodecs' 'kio' 'kross' 'kxmlgui' 'ki18n')
# Comment/uncomment the following dependencies to disable/enable support for
# MPV, gstreamer and/or Xine backend.
# These are not optdepends as they must be present at build time.
# Also ensure these deps aren't installed if you don't want to link against them
# because Subtitle Composer will always link against them if available.
#depends+=('mpv')
depends+=('gstreamer')
depends+=('xine-lib')
makedepends=('extra-cmake-modules' 'git')
conflicts=(${_name})
install=${_name}.install
optdepends=('mplayer: for MPlayer backend'
            'ruby: for scripting'
            'python: for scripting')
source=("git+https://github.com/maxrd2/${_name}.git")
md5sums=('SKIP')

pkgver() {
  export APP_VER=${pkgver}
  cd ${srcdir}/${_name}
  git describe --always | sed 's|-|.|g' | sed -e 's/^v//g'
}

build() {
  cd ${srcdir}/${_name}
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/${_name}
  make DESTDIR=${pkgdir} install
}
